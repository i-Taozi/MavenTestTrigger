apply plugin: "java"
apply plugin: "jetty"

gwt {
    gwtVersion='2.7.0' // Should match the gwt version used for building the gwt backend
    maxHeapSize="1G" // Default 256m is not enough for gwt compiler. GWT is HUNGRY
    minHeapSize="1G"

    src = files(file("src/")) // Needs to be in front of "modules" below.
    modules 'org.parallax3d.parallax.tests.ParallaxWebApp'
    devModules 'org.parallax3d.parallax.tests.ParallaxWebAppSuperdev'
    project.webAppDirName = 'webapp'

    compiler {
        strict = true;
        enableClosureCompiler = true;
        disableCastChecking = true;
    }
}

task draftRun(type: JettyRunWar) {
    dependsOn draftWar
    dependsOn.remove('war')
    webApp=draftWar.archivePath
    daemon=true
}

task superDev(type: de.richsource.gradle.plugins.gwt.GwtSuperDev) {
    dependsOn draftRun
    doFirst {
    	gwt.modules = gwt.devModules
    }
}

task dist(dependsOn: [clean, compileGwt]) {
    doLast {
        file("build/dist").mkdirs()
        copy {
            from "build/gwt/out"
            into "build/dist"
        }
        copy {
            from "webapp"
            into "build/dist"
            }
        copy {
            from "war"
            into "build/dist"
        }
    }
}

draftWar {
   from "war"
}

task addSource << {
    sourceSets.main.compileClasspath += files(project(':parallax').sourceSets.main.allJava.srcDirs)
    sourceSets.main.compileClasspath += files(project(':platforms:parallax-gwt').sourceSets.main.allJava.srcDirs)
    sourceSets.main.compileClasspath += files(project(':extensions:parallax-controllers').sourceSets.main.allJava.srcDirs)
    sourceSets.main.compileClasspath += files(project(':extensions:parallax-loaders').sourceSets.main.allJava.srcDirs)
    sourceSets.main.compileClasspath += files(project(':extensions:parallax-renderer-plugins').sourceSets.main.allJava.srcDirs)
    sourceSets.main.compileClasspath += files(project(':tests:parallax-tests').sourceSets.main.allJava.srcDirs)
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)

sourceCompatibility = 1.7
sourceSets.main.java.srcDirs = [ "src/" ]

